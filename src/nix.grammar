@top Nix { expression* }

expression { Mul | Div | Add | Sub | Int | Float | string | Identifier | List | ConcatList | AttrSet | Select | Apply | If | boolean | null | Braces | Function | Update | path }

@precedence {
  select @left,
  list @left,
  apply @left,
  negate @left,
  hasAttr @left,
  concatList @left,
  times @left,
  plus @left,
  not @left,
  update @left,
  compare @left,
  equal @left,
  and @left,
  or @left,
  imply @left
}

@skip { whitespace | CommentLine | CommentBlock }

@skip {} {
  // StringLine can be multiline, but then the indent is preserved
  // StringBlock is similar to python docstrings -> indent is removed
  string { StringLine | StringBlock }
  StringLine { '"' StringLineContent? '"' }
  StringBlock {
    stringBlockStart (StringBlockContent | StringBlockInterpolation)* stringBlockEnd
  }
}

// ''string ${expr} string''
StringBlockInterpolation { stringBlockInterpolationStart expression stringBlockInterpolationEnd }

Braces { "(" expression ")" }

Mul { expression !times "*" expression }
Div { expression !times "/" expression }

Add { expression (!plus "+" expression)+ }
Sub { expression !plus "-" expression }

List { "[" expression? (!list expression)* "]" }

//Function { Arguments ":" whitespace expression }
// FIXME Use of token whitespace conflicts with skip rule
// whitespace is required after :
Function { (Argument | Formals) ":" expression }

Argument { identifier }

Formals {
  FormalsAllBefore? "{" ((Formal? ("," Formal)* ("," FormalsRest)?) | FormalsRest) "}" |
  "{" ((Formal? ("," Formal)* ("," FormalsRest)?) | FormalsRest) "}" FormalsAllAfter?
}
FormalsAllBefore { Identifier "@" }
FormalsAllAfter { "@" Identifier }
FormalsRest { "..." }
Formal { Identifier FormalDefault? }
FormalDefault { "?" expression }

AttrSet { "{" (Attr)* "}" }

Attr { (string | Identifier | Select) "=" expression ";" }

// TODO no whitespace is allowed around the "."
// TODO parse default value
Select { (string | Identifier) "." AttrPath SelectDefault? }
SelectDefault { keyword<"or"> expression }

Apply { expression !apply expression }

AttrPath { (string | Identifier) ("." (string | Identifier))* }

path { PathAbsolute | PathRelative | PathLibrary }

ConcatList { List (!concatList "++" List)+ }

Update { expression (!update "//" expression)+ }

// TODO better
Identifier { identifier }

keyword<term> { @specialize[@name={term}]<identifier, term> }

boolean { @specialize[@name=BooleanLiteral]<identifier, "true" | "false"> }

null { @specialize[@name=NullLiteral]<identifier, "null"> }

If { keyword<"if"> expression keyword<"then"> expression keyword<"else"> expression }

@external tokens stringBlock from "./tokens" {
  StringBlockContent,
  stringBlockInterpolationStart,
  stringBlockEnd
}

@tokens {
  Int {
    std.digit+
  }

  Float {
    ((std.digit+ "." std.digit+) | ("." std.digit+)) (("e" | "E") std.digit+)?
  }
  // fix: Overlapping tokens Int and Float
  // fix: Overlapping tokens Float and "."
  @precedence { Float, Int, "." }

  StringLineContent { (!["] | "\\" _)+ }

  stringBlockStart { "''" }
  stringBlockInterpolationEnd { "}" }

  identifier { identifierChar (identifierChar | std.digit)* }
  identifierChar { std.asciiLetter | $[_$\u{a1}-\u{10ffff}] }

  // fix error: Overlapping tokens identifier and whitespace
  // TODO why overlapping? identifier is non-whitespace
  @precedence { identifier, whitespace }

  whitespace { std.whitespace+ }

  CommentLine { "#" ![\n]* }

  CommentBlock { "/*" blockCommentRest }
  blockCommentRest { ![*] blockCommentRest | "*" blockCommentAfterStar }
  blockCommentAfterStar { "/" | "*" blockCommentAfterStar | ![/*] blockCommentRest }

  PathAbsolute { "/" PathContent }
  // fix: Overlapping tokens "/" and PathAbsolute
  @precedence { PathAbsolute, "/" }

  PathRelative { "./" PathContent }
  // fix: Overlapping tokens PathRelative and "."
  @precedence { PathRelative, "." }

  PathLibrary { "<" PathContent ">" }

  // TODO produce extra token PathContent
  PathContent { identifier ("/" identifier)* }
}
